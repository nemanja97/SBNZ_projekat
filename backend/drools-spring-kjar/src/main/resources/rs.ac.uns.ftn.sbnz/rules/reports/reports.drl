package rs.ac.uns.ftn.sbnz.rules.reports;

import rs.ac.uns.ftn.sbnz.models.drools.PropertyWithScore;
import rs.ac.uns.ftn.sbnz.models.drools.ScoredProperties;
import rs.ac.uns.ftn.sbnz.models.*;
import rs.ac.uns.ftn.sbnz.models.Property;
import rs.ac.uns.ftn.sbnz.models.PlaceOfInterest;
import rs.ac.uns.ftn.sbnz.models.drools.SmartSearch;
import rs.ac.uns.ftn.sbnz.models.drools.PropertyInformation;
import rs.ac.uns.ftn.sbnz.models.drools.PersonalInformation;
import rs.ac.uns.ftn.sbnz.models.drools.FinancialReport;
import rs.ac.uns.ftn.sbnz.models.enums.Interest;
import rs.ac.uns.ftn.sbnz.models.enums.Heating;
import rs.ac.uns.ftn.sbnz.models.enums.PetStatus;
import rs.ac.uns.ftn.sbnz.models.enums.Amenity;
import rs.ac.uns.ftn.sbnz.models.enums.TypeOfPlace;
import rs.ac.uns.ftn.sbnz.models.enums.PropertyStatus;

import java.util.Date;
import java.util.List
import java.util.ArrayList;

rule "Financial report - basic information" salience 1
    agenda-group "reports"
    lock-on-active
    when
        $report: FinancialReport(
            $from: getFrom(),
            $to: getTo()
        )
        $properties: List() from collect(
            Property(status == PropertyStatus.SOLD,
            modifiedDate after $from,
            modifiedDate before $to)
        )
        accumulate(
            Property($price: price) from $properties,
		    $count: count($price),
		    $sum: sum($price),
		    $min: min($price),
		    $max: max($price),
		    $avg: average($price)
        )
    then
        modify($report){
            setAvgPrice($avg.doubleValue()),
            setMaxPrice($max.doubleValue()),
            setMinPrice($min.doubleValue()),
            setTotalPrice($sum.doubleValue()),
            setCount($count.intValue())
        };
        System.out.println($report);
end

rule "Financial report - properties" salience (-$dateModified.getTime() / 1000)
    agenda-group "reports"
    lock-on-active
    when
        $report: FinancialReport(
            $from: getFrom(),
            $to: getTo()
        )
        $property: Property(
            status == PropertyStatus.SOLD,
            modifiedDate after $from,
            modifiedDate before $to,
            $dateModified: modifiedDate
        )
    then
        System.out.println($property);
        List<Property> appendedList = new ArrayList<Property>($report.getPropertyList());
        appendedList.add($property);
        modify($report){
            setPropertyList(appendedList)
        }
end
